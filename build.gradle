group 'co.llective.hyena'
version '0.1-SNAPSHOT'

buildscript {
  ext.kotlin_version = '1.2.31'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"
  }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

mainClassName = "co.llective.hyena.repl.MainKt"

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}

repositories {
  mavenCentral()
  maven { url "http://clojars.org/repo/" }
  maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  compile group: 'org.python', name: 'jython-standalone', version: '2.5.2'
  compile group: 'com.google.guava', name: 'guava', version: '23.1-jre'
  compile group: 'io.airlift', name: 'log', version: '0.150'
  compile group: 'io.airlift', name: 'configuration', version: '0.150'
  compile group: 'io.airlift', name: 'bootstrap', version: '0.150'
  compile group: 'cs', name: 'jnanomsg', version: '0.4.5'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

  testCompile 'org.jetbrains.spek:spek-api:1.1.5'
  testCompile group: 'com.natpryce', name: 'hamkrest', version: '1.4.2.2'
  testCompile group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.5.0'
  testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.31'
  testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
  testRuntime 'org.junit.platform:junit-platform-runner:1.0.1'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId group
      artifactId project.name + '-all'

      pom.packaging 'jar'

      artifact fatJar {
      }
      artifact sourcesJar {
      }
    }
  }
  repositories {
    mavenLocal()
  }
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
  main.kotlin.srcDirs += 'src/main/kotlin/'
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

task sourcedJar(type: Jar) {
  from sourceSets.main.allSource
  with jar
}

task fatJar(type: Jar) {
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  baseName = project.name + '-all'
  with sourcedJar
}

task repledJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'Hyena API REPL',
            'Implementation-Version': version,
            'Main-Class': 'co.llective.hyena.repl.MainKt'
  }
  baseName = project.name + '-repl-all'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

task gentestJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Hyena API REPL',
                'Implementation-Version': version,
                'Main-Class': 'co.llective.hyena.util.GenTestKt'
    }
    baseName = project.name + '-gentest'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task parsemsgJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'Hyena API REPL',
            'Implementation-Version': version,
            'Main-Class': 'co.llective.hyena.util.ParseMsgKt'
  }
  baseName = project.name + '-parsemsg'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}